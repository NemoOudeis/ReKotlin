task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// maven artifact

apply plugin: 'maven-publish'

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        library(MavenPublication) {
            groupId project.group
            artifactId project.name // explicitly name project so the property won't be shadowed
            version project.version
            from components.java
            artifact(javadocJar)
            artifact(sourcesJar)
        }
    }
}

// publishing to jcenter
// see https://github.com/bintray/gradle-bintray-plugin
apply plugin: 'com.jfrog.bintray'

def user =  System.getenv('BINTRAY_USER')
def key =   System.getenv('BINTRAY_KEY')
def repo =  System.getenv('BINTRAY_REPO')
def org =   System.getenv('BINTRAY_ORG')

bintray {
    it.user = user
    it.key = key
    publications = publishing.publications.collect{ it.name }
    pkg {
        it.repo = repo
        name = project.name
        userOrg = org
        websiteUrl = 'https://github.com/rakutentech/ReKotlin'
        vcsUrl = 'https://github.com/rakutentech/ReKotlin.git'
        licenses = ["MIT"]
        publish = true
        version {
            name = project.version
            desc = project.description
            released = new Date()
            vcsTag = "${project.name}_v${project.version}"
        }
    }
}

task ensureBintrayCredentials {
  doFirst {
    if (!user?.trim() || !key?.trim() || !repo?.trim() || !org?.trim()) {
      throw new GradleException("You must define environment variables for " +
              "BINTRAY_USER, BINTRAY_KEY, BINTRAY_REPO and BINTRAY_ORG.")
    }
  }
}

bintrayUpload.dependsOn ensureBintrayCredentials
publish.dependsOn bintrayUpload